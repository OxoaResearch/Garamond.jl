#!/bin/julia

#########################################
# Garamond script for server operations #
#########################################
module GaramondServer

using Pkg
Pkg.activate(@__DIR__)
using Garamond
using Sockets
using Logging
using ArgParse


# Function that parses Garamond's server arguments
function get_server_commandline_arguments(args::Vector{String})
	s = ArgParseSettings()
	@add_arg_table s begin
        "--data-config", "-d"
            help = "data configuration file"
            action = :append_arg
        "--log-level"
            help = "logging level"
            default = "info"
        "--log", "-l"
            help = "logging stream"
            default = "stdout"
        "--unix-socket", "-u"
            help = "UNIX socket for data communication"
            arg_type = String
        "--web-socket-port", "-w"
            help = "WEB socket data communication port"
            arg_type = UInt16
        "--http-port", "-p"
            help = "HTTP port for REST services"
            arg_type = Int
	end
	return parse_args(args,s)
end


########################
# Main module function #
########################
Base.@ccallable function julia_main(ARGS::Vector{String})::Cint
    # Parse command line arguments
    args = get_server_commandline_arguments(ARGS)

    # Get the argument values
    log_level = args["log-level"]
    logging_stream = args["log"]

    # Logging
    logger = Garamond.build_logger(logging_stream, log_level)
    global_logger(logger)

    # Get ports
    unixsocket = args["unix-socket"]
    ws_port = args["web-socket-port"]
    http_port = args["http-port"]

    # Start Server #
    data_config_paths = String.(args["data-config"])
    if !isempty(data_config_paths)
        if unixsocket != nothing || ws_port != nothing || http_port != nothing
            Garamond.search_server(data_config_paths, unixsocket, ws_port, http_port)
        else
            @warn """At least a UNIX-socket, WEB-socket port or HTTP port
                     have to be specified through the -u, -w or -p switches.
                     Exiting..."""
        end
    else
        @warn """At least one data configuration file has to be provided
                 through the -d option. Exiting..."""
    end
    return 0
end


################################
# Start main Garamond function #
################################
julia_main(ARGS)

end # GaramondServer
